<prog> -> require "ifj21" <main-list>

<main-list> -> <def-declare-fcall><main-list>
<main-list>-> eps*

<statement-list> -> <statement><statement-list>
<statement-list> -> eps
<statement> -> id<assign-or-fc-call>
<statement> -> <declare-local>
<statement> -> <while>
<statement> -> <if>
<statement> -> <return>

<assign-or-fc-call> -> (<item-list>)
<assign-or-fc-call> -> <id-list> = <function-or-item-list>
<function-or-item-list> -> expr<item-another>
<function-or-item-list> -> id<fc-call-or-item-list>
<fc-call-or-item-list> -> (<item-list>)
<fc-call-or-item-list> -> <item-another>
<id-list> -> ,id<id-list>
<id-list> -> eps

<item> -> id
<item> -> expr

<return> -> return <item-list>
<item-list> -> <item> <item-another>
<item-list> -> eps
<item-another> -> , <item><item-another>
<item-another> -> eps

<def-declare-fcall> ->  global id:<func-or-type>
<def-declare-fcall> -> function id (<func-argument-list>) <return-types> <statement-list> end
<def-declare-fcall> -> id(<item-list>)

<declare-local> -> local id:<type><declare-assign>
<func-or-type> -> function(<types>)<return-types>
<func-or-type>-><type><declare-assign>
<declare-assign> -> = <item> 
<declare-assign> ->eps

<func-argument-list> -> <func-argument>  <func-argument-another>
<func-argument-list> -> eps
<func-argument-another> -> , <func-argument><func-argument-another>
<func-argument-another> -> eps
<func-argument> -> id:<type> 

<return-types> -> eps
<return-types> -> : <type><type-list>
<type-list> -> ,<type><type-list>
<type-list> -> eps
<types> -> <type><type-list>
<types> -> eps

<if> ->  if <if-while-expression> then <statement-list> else <statement-list> end

<while> -> while <if-while-expression> do <statement-list> end
<if-while-expression> -> <item><if-while-operator><item>

<type> -> integer
<type> -> string
<type> -> number

<if-while-operator> -> <
<if-while-operator> -> >
<if-while-operator> -> ==
<if-while-operator> -> ~=
<if-while-operator> -> <=
<if-while-operator> -> >=

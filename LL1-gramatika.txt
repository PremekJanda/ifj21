<prog> -> require "ifj21" <statement-list>

<statement-list> -> <statement><statement-list>
<statement-list> -> eps
<statement> -> <assign-or-fc-call> 
<statement> -> <declare>
<statement> -> <function-declare>
<statement> -> <while>
<statement> -> <if>
<statement> -> <built-in-func-write>
<statement> -> <return>

<assign-or-fc-call> -> id<assign-or-fc-call’> 
<assign-or-fc-call’> -> (<expr-list>)
<assign-or-fc-call’> -> = <expr-list>
<assign-or-fc-call’> -> <id-list> = <function-or-expr-list>
<function-or-expr-list> -> <expr’><expr-another>
<function-or-expr-list> -> id<expr-list-or-fc-call>
<expr-list-or-fc-call> -> (<expr-list>)
<expr-list-or-fc-call> -> <expr-another>
<function-or-expr-list> -> <built-in-func-call>
<id-list> -> ,id<id-another>
<id-another> -> ,id<id-another>
<id-another> -> eps

<expr’> -> integer_expr
<expr’> -> string_expr
<expr’> -> number_expr
<expr’> -> nil

<expr> -> id
<expr> -> integer_expr
<expr> -> string_expr
<expr> -> number_expr
<expr> -> nil

<return> -> return <expr-list>
<expr-list> -> <expr> <expr-another>
<expr-list> -> eps
<expr-another> -> , <expr><expr-another>
<expr-another> -> eps

<declare> -> local id:<type><declare-assign>
<declare> -> global id:<type><declare-assign>
<declare-assign> -> =<expr> 
<declare-assign> ->eps

<function-declare> -> function id (<func-assign_list>)  <return-types> <statement-list>  end
<func-assign-list> -> <func-assign>  <func-assign-another>
<func-assign-list> -> eps
<func-assign-another> -> , <func-assign><func-assign-another>
<func-assign-another> -> eps
<func-assign> -> id:<type> 

<return-types> -> eps
<return-types> -> : <type><type-list>
<type-list> -> ,<type><type-list>
<type-list> -> eps

<if> ->  if <if-while-expression> then <statement-list> else <statement-list> end

<while> -> while <if-while-expression> do <statement-list> end
<if-while-expression> -> <expr><if-while-operator><expr>

<built-in-func-write> -> write(<write-args-list>)
<write-args-list> -> <expr> <write-args-another>
<write-args-another> -> , <expr> <write-args-another>
<write-args-another> -> eps

<built-in-func-call> -> <built-func-read>
<built-in-func-call> -> <built-func-int>
<built-in-func-call> -> <built-func-substr>
<built-in-func-call> -> <built-func-ord>
<built-in-func-call> -> <built-func-chr>

<built-func-read> -> <reads>()
<reads> -> reads
<reads> -> readi
<reads> -> readn
<built-func-int> -> tointeger(<expr>)
<built-func-substr> -> substr(<expr>,<expr>,<expr>)
<built-func-ord> -> ord(<expr>,<expr>)
<built-func-chr> -> chr(<expr>)

<type> -> integer
<type> -> string
<type> -> number

<if-while-operator> -> <
<if-while-operator> -> >
<if-while-operator> -> ==
<if-while-operator> -> ~=
<if-while-operator> -> <=
<if-while-operator> -> >=
